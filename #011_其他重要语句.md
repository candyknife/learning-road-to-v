# 3.2 其他重要语句

## 3.2.1 `in` 操作符

`in` 用来检查一个数组或者哈希表中是否含有某一个元素。检查是否不含某元素用 `!in`。

    nums := [1, 2, 3]
    println(1 in nums) // true
    println(4 !in nums) // true
    m := {
        'one': 1
        'two': 2
    }
    println('one' in m) // true
    println('three' !in m) // true

在写布尔表达式时，`in` 操作符会使得表达式更清晰紧凑：

    enum Token {
        plus
        minus
        div
        mult
    }

    struct Parser {
        token Token
    }

    parser := Parser{}
    if parser.token == .plus || parser.token == .minus || parser.token == .div || parser.token == .mult {
        // ...
    }
    if parser.token in [.plus, .minus, .div, .mult] {
        // ...
    }

V 编译器会优化上面的表达式，上面代码中的两种 `if` 形式是完全等价的，第二种形式并不会创建新的数组。

## 3.2.2 `match` 语句

    os := 'windows'
    print('V is running on ')
    match os {
        'darwin' { println('macOS.') }
        'linux' { println('Linux.') }
        else { println(os) }
    }

`match` 语句比写上一系列的 `if - else` 语句短。当某个分支成功匹配时，后面的语句就会被执行。else 分支仅在其他分支都不匹配时执行。

    number := 2
    s := match number {
        1 { 'one' }
        2 { 'two' }
        else { 'many' }
    }

`match` 语句还能被用来实现 `if - else if - else` 的逻辑：

    match true {
        2 > 4 { println('if') }
        3 == 4 { println('else if') }
        2 == 2 { println('else if2') }
        else { println('else') }
    }
    // 'else if2' should be printed

或者 `unless` 的逻辑：

    match false {
        2 > 4 { println('if') }
        3 == 4 { println('else if') }
        2 == 2 { println('else if2') }
        else { println('else') }
    }
    // 'if' should be printed

`match` 语句会返回所匹配的分支上最终表达式的值。

    enum Color {
        red
        blue
        green
    }

    fn is_red_or_blue(c Color) bool {
        return match c {
            .red, .blue { true } // comma can be used to test multiple values
            .green { false }
        }
    }

`match` 语句的分支上还可以使用简短的 `.variant_here` 语法表达 `enum` 的变量。当所有分支覆盖了 `enum` 中的每一项时，不允许使用 `else` 分支。

    c := `v`
    typ := match c {
        `0`...`9` { 'digit' }
        `A`...`Z` { 'uppercase' }
        `a`...`z` { 'lowercase' }
        else { 'other' }
    }
    println(typ)
    // 'lowercase'

`match` 语句的分支上也可以写一些范围。如果值落在了一个分支的范围上，就会执行分支后的语句。

注意这里的范围应该使用 `...`（三点）表达，而非 `..`（两点）。因为此处范围应该是左右均闭合的，`..` 表示左闭右开的范围，在此处使用会报错。

注意：`match` 不能出现在 `for` 循环与 `if` 语句中。

## `defer` 语句

`defer` 语句让一些代码直到函数返回值后才执行。

    import os

    fn read_log() {
        mut ok := false
        mut f := os.open('log.txt') or { panic(err) }
        defer {
            f.close()
        }
        // ...
        if !ok {
            // defer statement will be called here, the file will be closed
            return
        }
        // ...
        // defer statement will be called here, the file will be closed
    }

如果函数返回一个表达式，`defer` 语句会在该表达式执行完成后执行。

    import os

    enum State {
        normal
        write_log
        return_error
    }

    // write log file and return number of bytes written
    fn write_log(s State) ?int {
        mut f := os.create('log.txt')?
        defer {
            f.close()
        }
        if s == .write_log {
            // `f.close()` will be called after `f.write()` has been
            // executed, but before `write_log()` finally returns the
            // number of bytes written to `main()`
            return f.writeln('This is a log file')
        } else if s == .return_error {
            // the file will be closed after the `error()` function
            // has returned - so the error message will still report
            // it as open
            return error('nothing written; file open: $f.is_opened')
        }
        // the file will be closed here, too
        return 0
    }

    fn main() {
        n := write_log(.return_error) or {
            println('Error: $err')
            0
        }
        println('$n bytes written')
    }
