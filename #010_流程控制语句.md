# 3.1 流程控制语句

## 3.1.1 分支结构 if

### if 语句与表达式

    a := 10
    b := 20
    if a < b {
        println('$a < $b')
    } else if a > b {
        println('$a > $b')
    } else {
        println('$a == $b')
    }

`if` 语句非常简单，与大多数其他语言都类似。但与其他类 C 语言不同，判断条件周围没有小括号，并且每个分支无论多少行代码必须要有大括号。

`if` 也可以用一个表达式书写：

    num := 777
    s := if num % 2 == 0 { 'even' } else { 'odd' }
    println(s)
    // "odd"

### 类型检查与传播

你可以使用 `is` 及其求反形式 `!is` 检查和类型的当前类型。

你也可以用 `if` 语句完成它：

    struct Abc {
        val string
    }

    struct Xyz {
        foo string
    }

    type Alphabet = Abc | Xyz

    x := Alphabet(Abc{'test'}) // sum type
    if x is Abc {
        // x is automatically casted to Abc and can be used here
        println(x)
    }
    if x !is Abc {
        println('Not Abc')
    }

或者使用 `match` 语句：

    match x {
        Abc {
            // x is automatically casted to Abc and can be used here
            println(x)
        }
        Xyz {
            // x is automatically casted to Xyz and can be used here
            println(x)
        }
    }

这对结构体内部也可作用：

    struct MyStruct {
        x int
    }

    struct MyStruct2 {
        y string
    }

    type MySumType = MyStruct | MyStruct2

    struct Abc {
        bar MySumType
    }

    x := Abc{
        bar: MyStruct{123} // MyStruct will be converted to MySumType type automatically
    }
    if x.bar is MyStruct {
        // x.bar is automatically casted
        println(x.bar)
    } else if x.bar is MyStruct2 {
        new_var := x.bar as MyStruct2
        // ... or you can use `as` to create a type cast an alias manually:
        println(new_var)
    }
    match x.bar {
        MyStruct {
            // x.bar is automatically casted
            println(x.bar)
        }
        else {}
    }

可变变量的值会改动，因此传播操作并不安全。尽管如此，有时类型传播有其必要的作用，这种情况下开发者必须在表达式前用 `mut` 关键字来告诉编译器，他知道自己在做什么。

像下面这样：

    mut x := MySumType(MyStruct{123})
    if mut x is MyStruct {
        // x is casted to MyStruct even if it's mutable
        // without the mut keyword that wouldn't work
        println(x)
    }
    // same with match
    match mut x {
        MyStruct {
            // x is casted to MyStruct even if it's mutable
            // without the mut keyword that wouldn't work
            println(x)
        }
    }

## 3.1.2 循环结构 for

V 只有一种循环关键字 `for`，但有多种不同形式。

### `for`/`in`

这是最常见的形式，你可以在数组、哈希表或数字范围中使用。

### 数组循环

    numbers := [1, 2, 3, 4, 5]
    for num in numbers {
        println(num)
    }
    names := ['Sam', 'Peter']
    for i, name in names {
        println('$i) $name')
        // Output: 0) Sam
        //         1) Peter
    }

`for value in arr` 的形式用来遍历一个数组中的每个元素。如果同时还需要索引，则可以使用 `for index, value in arr`。

注意：这里的 value 是只读的。如果在循环中你要更改值，则需要将所有元素都声明为可变的：

    mut numbers := [0, 1, 2]
    for mut num in numbers {
        num++
    }
    println(numbers) // [1, 2, 3]

当标识符是下划线时，将忽略。

### 自定义迭代器 iterators

实现了 `next` 方法来返回 `Option` 的类型可以用 `for` 循环来迭代。

    struct SquareIterator {
        arr []int
    mut:
        idx int
    }

    fn (mut iter SquareIterator) next() ?int {
        if iter.idx >= iter.arr.len {
            return error('')
        }
        defer {
            iter.idx++
        }
        return iter.arr[iter.idx] * iter.arr[iter.idx]
    }

    nums := [1, 2, 3, 4, 5]
    iter := SquareIterator{
        arr: nums
    }
    for squared in iter {
        println(squared)
    }

输出结果为：

    1
    4
    9
    16
    25

### 哈希表循环

    m := {
        'one': 1
        'two': 2
    }
    for key, value in m {
        println('$key -> $value')
        // Output: one -> 1
        //         two -> 2
    }

key 或者 value 都可以用下划线替代而忽略。

    m := {
        'one': 1
        'two': 2
    }
    // iterate over keys
    for key, _ in m {
        println(key)
        // Output: one
        //         two
    }
    // iterate over values
    for _, value in m {
        println(value)
        // Output: 1
        //         2
    }

### 数字范围循环

    // Prints '01234'
    for i in 0 .. 5 {
        print(i)
    }

`low..high` 意思是一个独立的范围，包括该范围内**左闭右开**的所有值。

### 条件循环

    mut sum := 0
    mut i := 0
    for i <= 100 {
        sum += i
        i++
    }
    println(sum) // "5050"

这种形式与其他语言里的 `while` 循环相似。只有当条件语句为 False 时才会停止循环。与 `if` 一样，条件语句不需要小括号，循环内容一定要用大括号。

### 无限循环

    mut num := 0
    for {
        num += 2
        if num >= 10 {
            break
        }
    }
    println(num) // "10"

循环条件可以省略，此时为无限循环。

### C 循环

    for i := 0; i < 10; i += 2 {
        // Don't print 6
        if i == 6 {
            continue
        }
        println(i)
    }

最后是 C 语言风格的 `for` 循环。这比 `while` 形式的安全，因为后者很容易因为忘记写迭代条件更新计数而导致死循环。

这里 `i` 不需要用 `mut` 声明，因为它就是定义为可变的。

### 带标记的 break 与 continue

`break` 与 `continue` 默认只控制所在的最内层 `for` 循环。你也可以在 `break` 与 `continue` 后面增加标记来跳到更外层的 `for` 循环：

    outer: for i := 4; true; i++ {
        println(i)
        for {
            if i < 7 {
                continue outer
            } else {
                break outer
            }
        }
    }

标记必须紧跟在外部循环前。上面代码会打印：

    4
    5
    6
    7
