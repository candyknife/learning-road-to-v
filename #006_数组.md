# 2.4 数组

## 2.4.1 数组的定义

数组是许多同一类型数据元素的集合。数组由方括号括起来的表达式列表表示。每个元素可以通过索引得到。索引从 0 开始：

    mut nums := [1, 2, 3]
    println(nums) // `[1, 2, 3]`
    println(nums[0]) // `1`
    println(nums[1]) // `2`

    nums[1] = 5
    println(nums) // `[1, 5, 3]`

可以通过 push 操作 `<<` 往一个数组的末尾添加元素。也可以直接添加一整个数组。

    mut nums := [1, 2, 3]
    nums << 4
    println(nums) // "[1, 2, 3, 4]"

    // append array
    nums << [5, 6, 7]
    println(nums) // "[1, 2, 3, 4, 5, 6, 7]"

对于字符串数组也有类似操作。

    mut names := ['John']
    names << 'Peter'
    names << 'Sam'
    // names << 10  <-- This will not compile. `names` is an array of strings.

`val in array` 语句返回数组 array 是否包含值 val 的布尔值。进一步的内容参考[`in` operator](https://github.com/vlang/v/blob/master/doc/docs.md#in-operator)。

    names := ['John', 'Peter', 'Sam']
    println('Alex' in names) // "false"

## 2.4.2 数组的字段

有两个字段控制数组的“尺寸”：

- `len`: length - 数组初始化与预赋值的元素数目。
- `cap`: capacity - 没有初始化但为元素保留的内存空间大小。数组可以在不经重新分配的情况下增长到这一大小。通常情况下 V 会自动分配，后面一节介绍手动调整的方法。

    mut nums := [1, 2, 3]
    println(nums.len) // "3"
    println(nums.cap) // "3" or greater
    nums = [] // The array is now empty
    println(nums.len) // "0"

`data` 是 `voidptr` 类型的字段，内容为第一个元素的地址，仅在较低层的 `unsafe` 代码中才会用到。

注意：字段都是只读的，不能被用户修改。

## 2.4.3 数组的初始化

数组的类型由第一个元素决定：

- `[1, 2, 3]` is an array of ints (`[]int`).
- `['a', 'b']` is an array of strings (`[]string`).

用户可以显式指定第一个元素的类型：`[u8(16), 32, 64, 128]`。V 中数组是同构的（所有元素必须类型相同）。这意味着 `[1, 'a']` 这样的代码无法通过编译。

上面的句法初始化少量已知元素尚可，但想初始化一个非常大的或者空数组时，可以用如下句法：

    mut a := []int{len: 10000, cap: 30000, init: 3}

这将创建一个 10000 个 `int` 元素的数组，初始值均为 `3`。内存空间保留到 30000 个元素。

- `len`: 可选，默认为 0
- `cap`: 系统自动保证大于等于 `len`
- `init`: 数字数组默认为 `0`，字符串数组默认为 `''`

    arr := []int{len: 5, init: -1}
    // `arr == [-1, -1, -1, -1, -1]`, arr.cap == 5

    // Declare an empty array:
    users := []int{}

设置容量可以提高将元素 push 到数组的性能，因为可以避免重新分配数组：

    mut numbers := []int{cap: 1000}
    println(numbers.len) // 0
    // Now appending elements won't reallocate
    for i in 0 .. 1000 {
        numbers << i
    }

注意：上面代码用了 range for 语句

你可以通过 `it` 变量获取索引来初始化数组：

    count := []int{len: 4, init: it}
    assert count == [0, 1, 2, 3]

    mut square := []int{len: 6, init: it * it}
    // square == [0, 1, 4, 9, 16, 25]

## 2.4.4 数组类型

数组可以为以下类型:

- Number:	`[]int,[]i64`
- String:	`[]string`
- Rune:	`[]rune`
- Boolean:	`[]bool`
- Array:	`[][]int`
- Struct:	`[]MyStructName`
- Channel:	`[]chan f64`
- Function:	`[]MyFunctionType []fn (int) bool`
- Interface:	`[]MyInterfaceName`
- Sum Type:	`[]MySumTypeName`
- Generic Type:	`[]T`
- Map:	`[]map[string]f64`
- Enum:	`[]MyEnumType`
- Alias:	`[]MyAliasTypeName`
- Thread:	`[]thread int`
- Reference:	`[]&f64`
- Shared:	`[]shared MyStructType`

示例代码:

本例使用结构体和求和类型创建一个数组，该数组可以处理不同类型（如点、线）的数据元素。

    struct Point {
        x int
        y int
    }

    struct Line {
        p1 Point
        p2 Point
    }

    type ObjectSumType = Line | Point

    mut object_list := []ObjectSumType{}
    object_list << Point{1, 1}
    object_list << Line{
        p1: Point{3, 3}
        p2: Point{4, 4}
    }
    dump(object_list)
    /*
    object_list: [ObjectSumType(Point{
        x: 1
        y: 1
    }), ObjectSumType(Line{
        p1: Point{
            x: 3
            y: 3
        }
        p2: Point{
            x: 4
            y: 4
        }
    })]
    */

## 2.4.5 多维数组

数组可以超过一个维度。

2d 数组：

    mut a := [][]int{len: 2, init: []int{len: 3}}
    a[0][1] = 2
    println(a) // [[0, 2, 0], [0, 0, 0]]

3d 数组：

    mut a := [][][]int{len: 2, init: [][]int{len: 3, init: []int{len: 2}}}
    a[0][1][1] = 2
    println(a) // [[[0, 0], [0, 2], [0, 0]], [[0, 0], [0, 0], [0, 0]]]

## 2.4.6 定长数组

V 支持定长数组。与普通数组不同，定长数组的长度是恒定的，不能向里增加元素也不能压缩数组。仅能修改里面的元素。

但是，访问定长数组的元素更高效，它们比普通数组需要更少的内存，并且与普通数组不同，它们的数据在堆栈上，因此如果不需要额外的堆分配，可以将它们用作缓冲区。

只是多数方法只在普通数组上工作，不适用于定长数组。你可以通过切片将定长数组转换为普通数组：

    mut fnums := [3]int{} // fnums is a fixed size array with 3 elements.
    fnums[0] = 1
    fnums[1] = 10
    fnums[2] = 100
    println(fnums) // => [1, 10, 100]
    println(typeof(fnums).name) // => [3]int

    fnums2 := [1, 10, 100]! // short init syntax that does the same (the syntax will probably change)

    anums := fnums[..] // same as `anums := fnums[0..fnums.len]`
    println(anums) // => [1, 10, 100]
    println(typeof(anums).name) // => []int

注意：切片会将定长数组的数据复制一份到新创建的普通数组中。
