# 4.1 结构体

## 4.1.1 结构体的定义与声明

    struct Point {
        x int
        y int
    }

    mut p := Point{
        x: 10
        y: 20
    }
    println(p.x) // Struct fields are accessed using a dot
    // Alternative literal syntax for structs with 3 fields or fewer
    p = Point{10, 20}
    assert p.x == 10

### 堆结构体

结构体一般都在栈中建立。想要在堆中建立一个结构体，并且得到一个引用，可以使用 `&` 前缀：

    struct Point {
        x int
        y int
    }

    p := &Point{10, 10}
    // References have the same syntax for accessing fields
    println(p.x)

`p` 的类型是 `&Point`，这是对 `Point` 的一个引用。引用类似于 Go 指针与 C++ 引用。

    struct Foo {
    mut:
        x int
    }

    fa := Foo{1}
    mut a := fa
    a.x = 2
    assert fa.x == 1
    assert a.x == 2

    // fb := Foo{ 1 }
    // mut b := &fb  // error: `fb` is immutable, cannot have a mutable reference to it
    // b.x = 2

    mut fc := Foo{1}
    mut c := &fc
    c.x = 2
    assert fc.x == 2
    assert c.x == 2
    println(fc) // Foo{ x: 2 }
    println(c) // &Foo{ x: 2 } // Note `&` prefixed.

## 4.1.2 结构体字段的默认值

    struct Foo {
        n   int    // n is 0 by default
        s   string // s is '' by default
        a   []int  // a is `[]int{}` by default
        pos int = -1 // custom default value
    }

结构体在创建的时候，所有字段都会设成默认值。数组与哈希表会完成内存分配。你也可以自定义默认值。

### 必填字段

    struct Foo {
        n int [required]
    }

你可以用 `[required]` 来标记一个结构体字段，这回告诉 V 该字段在创建一个该结构体的实例时是个必填项。

下面这个例子不会通过编译，因为字段 `n` 没有显式地初始化：

    _ = Foo{}

## 4.1.3 短结构体语法

    struct Point {
        x int
        y int
    }

    mut p := Point{
        x: 10
        y: 20
    }
    p = Point{
        x: 30
        y: 4
    }
    assert p.y == 4
    //
    // array: first element defines type of array
    points := [Point{10, 20}, Point{20, 30}, Point{40, 50}]
    println(points) // [Point{x: 10, y: 20}, Point{x: 20, y: 30}, Point{x: 40,y: 50}]

返回一个结构体，或者把结构体当成函数参数传递时，可以省略结构体名。

### 跟随结构体形式的参数 Trailing struct literal arguments

V 没有默认函数参数或者命名的参数，可以用跟随结构体语法来实现：

    [params]
    struct ButtonConfig {
        text        string
        is_disabled bool
        width       int = 70
        height      int = 20
    }

    struct Button {
        text   string
        width  int
        height int
    }

    fn new_button(c ButtonConfig) &Button {
        return &Button{
            width: c.width
            height: c.height
            text: c.text
        }
    }

    button := new_button(text: 'Click me', width: 100)
    // the height is unset, so it's the default value
    assert button.height == 20

可以看到，结构体的名字和括号都省略了，上面的简洁形式替换了：

    new_button(ButtonConfig{text:'Click me', width:100})

这只能用于那些把结构体作为最后一个参数的函数。

注意：`[param]` 标签用来告诉 V 后面跟着的结构体可以被完全省略，这样你才可以写 `button := new_button()`。如果没有它，你必须至少写出一个字段名，即使它有一个默认的参数值。否则在你不填任何参数调用函数时，编译器会报错 `error: expected 1 arguments, but got 0`。

## 4.1.4 访问修饰符 Access modifiers

结构体字段默认是私有与不可更改的（也会让结构体不可更改）。但访问修饰符可以修改访问权限。一共有以下五种选项：

    struct Foo {
        a int // private immutable (default)
    mut:
        b int // private mutable
        c int // (you can list multiple fields with the same access modifier)
    pub:
        d int // public immutable (readonly)
    pub mut:
        e int // public, but mutable only in parent module
    __global:
        // (not recommended to use, that's why the 'global' keyword starts with __)
        f int // public and mutable both inside and outside parent module
    }

私有字段只在同一个模块内可访问，其他模块想要直接访问就会在编译时报错。公开但不可变的字段就是任何地方只可读。

## 4.1.4 匿名结构体

V 支持匿名结构体：不需要用一个结构体名来单独声明的结构体。

    struct Book {
        author struct  {
            name string
            age  int
        }

        title string
    }

    book := Book{
        author: struct {
            name: 'Samantha Black'
            age: 24
        }
    }
    assert book.author.name == 'Samantha Black'
    assert book.author.age == 24

## 4.1.5 结构体方法

    struct User {
        age int
    }

    fn (u User) can_register() bool {
        return u.age > 16
    }

    user := User{
        age: 10
    }
    println(user.can_register()) // "false"
    user2 := User{
        age: 20
    }
    println(user2.can_register()) // "true"

V 没有类，但是你可以基于类型定义方法。方法就是具有特殊接收者参数的函数。接收者参数都在关键字 `fn` 与方法名之间的接收者参数列表中。方法必须写在接收者所在的模块里。

这个例子里，`can_register` 方法有一个 `User` 类型、名称为 `u` 的接收者。此处的惯例不是使用像 `self` 或 `this` 的接收者名称，而是使用简短的，最好是只有一个字母长的名称。


## 4.1.6 嵌入结构体

V 支持嵌入结构体。

    struct Size {
    mut:
        width  int
        height int
    }

    fn (s &Size) area() int {
        return s.width * s.height
    }

    struct Button {
        Size
        title string
    }

通过嵌入，结构体 `Button` 会自动获得结构体 `size` 中的全部字段与方法，这样你就可以：

    mut button := Button{
        title: 'Click me'
        height: 2
    }

    button.width = 3
    assert button.area() == 6
    assert button.Size.area() == 6
    print(button)

输出：

    Button{
        Size: Size{
            width: 3
            height: 2
        }
        title: 'Click me'
    }

与**继承**不同，不能在结构体和嵌入结构体之间进行类型转换（嵌入结构体也可以有自己的字段，也可以再嵌入多个结构体）。

如果你想要直接访问嵌入结构体，需要像这样清晰引用：`button.Size`。

从概念上讲，嵌入结构体类似于 OOP 中的**混入**（mixins），而不是基本的类。

你可以初始化一个嵌入结构体：

    mut button := Button{
        Size: Size{
            width: 3
            height: 2
        }
    }

或者更改值：

    button.Size = Size{
        width: 4
        height: 5
    }

如果多个嵌入结构体里有同名的方法或字段，或者在结构体中定义了同名的方法或字段，则可以更细致地对嵌入结构体调用方法或分配值，比如 `button.Size.area()`。如果未指定嵌入结构体名称，则定位到最外层结构体的同名方法。

## 4.1.7 联合体

就像结构体一样，联合体也支持嵌入。

    struct Rgba32_Component {
        r byte
        g byte
        b byte
        a byte
    }

    union Rgba32 {
        Rgba32_Component
        value u32
    }

    clr1 := Rgba32{
        value: 0x008811FF
    }

    clr2 := Rgba32{
        Rgba32_Component: Rgba32_Component{
            a: 128
        }
    }

    sz := sizeof(Rgba32)
    unsafe {
        println('Size: ${sz}B,clr1.b: $clr1.b,clr2.b: $clr2.b')
    }

输出：`Size: 4B, clr1.b: 136, cl2.b: 0`

联合体成员的访问必须出现在 `unsafe` 块中。

注意：嵌入结构体的参数不一定按列出的顺序储存。
