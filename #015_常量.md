# 4.3 常量

## 4.3.1 常量的定义

    const (
        pi    = 3.14
        world = '世界'
    )

    println(pi)
    println(world)

常量通过 `const` 来定义。它们只能定义在模块级别（函数外面）。常量的值永远不会改变。除了像上面那样一起定义多个常量，你也可以像下面这样单独定义一个常量：

    const e = 2.71828

V 的常量比其他多数语言都要灵活许多。你可以定义更多复杂的值：

    struct Color {
        r int
        g int
        b int
    }

    fn rgb(r int, g int, b int) Color {
        return Color{
            r: r
            g: g
            b: b
        }
    }

    const (
        numbers = [1, 2, 3]
        red     = Color{
            r: 255
            g: 0
            b: 0
        }
        // evaluate function call at compile-time*
        blue = rgb(0, 0, 255)
    )

    println(numbers)
    println(red)
    println(blue)

因为正常情况下 V 不允许全局变量，因此常量会很有用。

## 4.3.2 模块中的公开常量

可以通过 `pub const` 使常量公开（可被外部调用读取展示）：

    module mymodule

    pub const golden_ratio = 1.61803

    fn calc() {
        println(mymodule.golden_ratio)
    }

`pub` 关键字必须在 `const` 关键字之前，并且不能用在一个 `const()` 块里。

## 4.3.3 常量的命名与使用规范

命名常量时，必须使用 `snake_case` 规范。

在使用常量时，为了与其他本地变量区别开，必须明确写出常量所在的完整路径。举个例子，为了访问 PI 常量，无论是模块内使用还是模块外使用都需要完整地写出 `math.pi`。

只有 `main` 模块是例外，`main` 函数可以直接用名称访问该文件中定义的常量。比如对于 `main` 文件里定义的常量 `numbers`，直接使用其名称即可，而不需要使用 `main.numbers`。

vfmt 会按这样的规则执行，所以你可以在 `math` 模块中写 `println(pi)`，但会被 vfmt 自动转换成 `println(math.pi)`。

在模块之外，模块里的全部常量都需要加上模块名的前缀。
